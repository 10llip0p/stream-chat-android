apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'androidx.navigation.safeargs.kotlin'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'org.jlleitschuh.gradle.ktlint'

buildscript {
    repositories {
        jcenter()
        google()
    }
    dependencies {
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:2.3.0"
    }
}

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.3"

    defaultConfig {
        applicationId "io.getstream.chat.sample"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode gitVersioner.versionCode
        versionName gitVersioner.versionName
        ndkVersion "21.3.6528147"
    }

    File signFile = rootProject.file('sample/.sign/keystore.properties')
    if (signFile.exists()) {
        Properties properties = new Properties()
        properties.load(new FileInputStream(signFile))
        signingConfigs {
            release {
                storeFile rootProject.file(properties['keystore'])
                storePassword properties['storePassword']
                keyAlias properties['keyAlias']
                keyPassword properties['keyPassword']
            }
        }
    } else {
        signingConfigs {
            release {
                storeFile rootProject.file('sample/.sign/debug.keystore')
                storePassword 'android'
                keyAlias 'androiddebugkey'
                keyPassword 'android'
            }
        }
    }

    signingConfigs.debug {
        storeFile rootProject.file('sample/.sign/debug.keystore')
        storePassword 'android'
        keyAlias 'androiddebugkey'
        keyPassword 'android'
    }

    buildTypes {
        debug {
            versionNameSuffix "-DEBUG"
            applicationIdSuffix ".debug"
            testCoverageEnabled true
            debuggable true
            minifyEnabled false
            shrinkResources false
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    kotlinOptions.jvmTarget = JavaVersion.VERSION_1_8.toString()

    sourceSets.all {
        it.java.srcDir "src/$it.name/kotlin"
    }

    lintOptions {
        abortOnError false
        xmlReport true
        checkDependencies true
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
        exclude("META-INF/*.kotlin_module")
    }
    
    dataBinding {
        enabled = true
    }
}

dependencies {
    implementation project(":library")
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"

    implementation "androidx.core:core-ktx:$ktxCoreVersion"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycleExtensionsVersion"
    implementation "androidx.appcompat:appcompat:$appcompatVersion"
    implementation "androidx.activity:activity-ktx:$activityKtxVersion"
    implementation "androidx.fragment:fragment-ktx:$fragmentKtxVersion"
    implementation "com.google.android.material:material:$materialVersion"
    implementation "com.google.firebase:firebase-analytics-ktx:$firebaseAnalyticsVersion"
    implementation "com.google.firebase:firebase-crashlytics:$firebaseCrashlyticsVersion"

    implementation "androidx.navigation:navigation-runtime-ktx:$navigationVersion"
    implementation "androidx.navigation:navigation-fragment-ktx:$navigationVersion"
    implementation "androidx.navigation:navigation-ui-ktx:$navigationVersion"

    implementation "androidx.preference:preference-ktx:$activityKtxVersion"

    implementation "androidx.constraintlayout:constraintlayout:1.1.3"

    def koinVersion = '2.1.5'
    def koin = [
            libs: [
                    "org.koin:koin-core:$koinVersion",
                    "org.koin:koin-core-ext:$koinVersion",
                    "org.koin:koin-androidx-scope:$koinVersion",
                    "org.koin:koin-androidx-viewmodel:$koinVersion",
                    "org.koin:koin-androidx-ext:$koinVersion"
            ],
            test: [
                    "org.koin:koin-test:$koinVersion"
            ]
    ]
    implementation koin.libs

    implementation 'com.jakewharton.timber:timber:4.7.1'

    def glideVersion = '4.9.0'
    implementation "com.github.bumptech.glide:glide:$glideVersion"
    annotationProcessor "com.github.bumptech.glide:compiler:$glideVersion"

    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.3'

    testImplementation 'junit:junit:4.12'
    testImplementation koin.test
}